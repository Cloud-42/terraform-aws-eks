# -----------------------------------------------------------
# Cluster SG 
# -----------------------------------------------------------
resource "aws_security_group" "cluster" {
  name        = "${var.eks_cluster_name}-cluster-sg"
  description = "${var.eks_cluster_name}-cluster-sg"
  vpc_id      = var.vpc_id

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "${var.eks_cluster_name}-cluster-sg"
  }
}

# -----------------------------------------------------------
# Allow access to Control plane from management IP
# -----------------------------------------------------------

resource "aws_security_group_rule" "cluster_sg_ingress" {
  cidr_blocks       = [var.management_cidr]
  description       = "Allow management IP to API Server"
  from_port         = 443
  protocol          = "tcp"
  security_group_id = aws_security_group.cluster.id
  to_port           = 443
  type              = "ingress"
}

# -----------------------------------------------------------
# Node SG
# -----------------------------------------------------------
resource "aws_security_group" "node" {
  name        = "${var.eks_cluster_name}-node-sg"
  description = "${var.eks_cluster_name}-node-sg"
  vpc_id      = var.vpc_id

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    "Name"                                          = "${var.eks_cluster_name}-node",
    "kubernetes.io/cluster/${var.eks_cluster_name}" = "owned",
  }
}

resource "aws_security_group_rule" "node_ingress_self" {
  description              = "Allow nodes to communicate with each other"
  from_port                = 0
  protocol                 = "-1"
  security_group_id        = aws_security_group.node.id
  source_security_group_id = aws_security_group.node.id
  to_port                  = 65535
  type                     = "ingress"
}

resource "aws_security_group_rule" "node_ingress_cluster" {
  description              = "Allow worker Kubelets and pods to receive communication from the cluster control plane"
  from_port                = 1025
  protocol                 = "tcp"
  security_group_id        = aws_security_group.node.id
  source_security_group_id = aws_security_group.cluster.id
  to_port                  = 65535
  type                     = "ingress"
}
# -----------------------------------------------------------
# K8S API Server Communication
# -----------------------------------------------------------
resource "aws_security_group_rule" "cluster_ingress_node_https" {
  description              = "Allow pods to communicate with the cluster API Server"
  from_port                = 443
  protocol                 = "tcp"
  security_group_id        = aws_security_group.cluster.id
  source_security_group_id = aws_security_group.node.id
  to_port                  = 443
  type                     = "ingress"
}
